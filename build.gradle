plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

group = 'com.magmaguy'
version = '1.7.0'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    //MagmaCore
    maven {
        url = 'http://magmaguy.com:50001/releases'
        allowInsecureProtocol = true
    }

    maven {
        name = "magmaguyRepoSnapshots"
        url = uri("http://magmaguy.com:50001/snapshots")
        allowInsecureProtocol = true;
    }

    //Bstats API
    maven { url = 'https://repo.codemc.org/repository/maven-public' }

    //WorldEdit API
    maven { url = 'https://maven.enginehub.org/repo/' }

    // carm start - Support MMOItems and MythicMobs
    //MMOItems
    maven { url = 'https://nexus.phoenixdevt.fr/repository/maven-public/' }
    //MythicMobs
    maven { url = 'https://mvn.lumine.io/repository/maven-public/' }
    // carm end - Support MMOItems and MythicMobs
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT'
    implementation group: 'com.magmaguy', name: 'MagmaCore', version: '1.0-SNAPSHOT'
    //BStats API
    implementation group: 'org.bstats', name: 'bstats-bukkit', version: '2.2.1'
    //WorldGuard API
    compileOnly group: 'com.sk89q.worldguard', name: 'worldguard-bukkit', version: '7.0.7'
    //WorldEdit API
    compileOnly group: 'com.sk89q.worldedit', name: 'worldedit-bukkit', version: '7.2.14'
    //lombok
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    //apache commons
    implementation 'org.apache.commons:commons-lang3:3.9'
    //reflections - used for config initialization
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.12'
    //EliteMobs
    compileOnly group: 'com.magmaguy', name: 'EliteMobs', version: '9.1.0'

    // carm start - Support MMOItems and MythicMobs
    //MythicMobs
    compileOnly group: 'io.lumine', name: 'Mythic-Dist', version: '5.2.1'
    //MythicLib
    compileOnly group: 'io.lumine', name: 'MythicLib-dist', version: '1.5.2-SNAPSHOT'
    //MMOItems
    compileOnly group: 'net.Indyuce', name: 'MMOItems-API', version: '6.9.2-SNAPSHOT'
    // carm end - Support MMOItems and MythicMobs

}

artifacts { // task 'build' runs generates uberjar
    archives shadowJar
}

jar {
    archiveClassifier.set('min') // we want the Uberjar to be distributed, this is the minified version
}

String packagePath = 'com.magmaguy.shaded'

// Relocating a Package
shadowJar {
    dependencies {
        relocate('org.bstats', packagePath + '.bstats')
    }
    archiveClassifier.set(null)
    archiveFileName.set(project.name + ".jar")
    destinationDirectory.set(new File("testbed/plugins"))
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

ext {
    resourceTokens = [
            'Version': version
    ]
}

processResources {
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: resourceTokens
}

publishing {
    repositories {
        maven {
            name = "BetterStructures"
            url = "http://magmaguy.com:50001/releases"
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
            allowInsecureProtocol = true
        }
    }

    publications {
        mavenJava(MavenPublication) {
            pom {
                groupId = 'com.magmaguy'
                name = 'betterstructures'
                description = 'BetterStructures repo'
                url = 'https://magmaguy.com/'
                from components.java
                licenses {
                    license {
                        name = 'GPLV3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
            }
        }
    }
}